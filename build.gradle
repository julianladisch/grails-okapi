//configurations.all {
//  // Check for updates every build
//  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
//}

buildscript {
  repositories {
    jcenter()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "http://maven.k-int.com/content/repositories/releases" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath "org.grails.plugins:hibernate5:${gormHibernate-".RELEASE"}"
    classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.0"
    classpath "org.grails.plugins:views-gradle:2.0.2"
    classpath "com.k_int.gradle:kint-gradle-plugin:2.0.0"
  }
}

//version "4.0.0"
group "com.k_int.okapi"

apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-plugin-publish"
apply plugin: "org.grails.plugins.views-json"
apply plugin: "com.k_int.gradle.plugin"
apply plugin: "com.k_int.gradle.conventional-git"

eclipse {
  classpath {
    defaultOutputDir = new File(project.buildDir.canonicalPath, 'main')
    file {
      whenMerged { classpath ->

        entries.collect().each {

          if (it.path =='grails-app/conf' && it.output?.toLowerCase().endsWith('test')) {
            classpath.entries.remove( it )
            println "Removed ${it}"
          }

          if (it.hasProperty('output') && it.output.startsWith('bin/')) {
            it.output = 'build/' + it.output.substring(4)
          }
        }
      }
    }
  }
}

repositories {
//  mavenLocal()
  jcenter()
  maven { url "https://repo.grails.org/grails/core" }
  maven { url "https://dl.bintray.com/grails/plugins" }
  maven { url "http://maven.k-int.com/content/repositories/releases" }
}

configurations {
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
}

dependencies {
  developmentOnly("org.springframework.boot:spring-boot-devtools")
  provided "org.springframework.boot:spring-boot-starter-logging"
  provided "org.springframework.boot:spring-boot-autoconfigure"
  provided "org.grails:grails-core"
  provided "org.springframework.boot:spring-boot-starter-undertow"
  provided "org.grails:grails-plugin-url-mappings"
  provided "org.grails:grails-plugin-rest"
  provided "org.grails:grails-plugin-codecs"
  provided "org.grails:grails-plugin-interceptors"
  provided "org.grails:grails-plugin-services"
  provided "org.grails:grails-plugin-datasource"
//  provided "org.grails:grails-plugin-databinding"
  //    provided "org.grails:grails-plugin-async"
  provided "org.grails:grails-web-boot"
  provided "org.grails:grails-logging"
//  provided "org.grails.plugins:cache" 
  provided "org.grails.plugins:hibernate5"
  compileOnly "io.micronaut:micronaut-inject-groovy"
  testCompile "io.micronaut:micronaut-inject-groovy"
  console "org.grails:grails-console"
  profile "org.grails.profiles:rest-api-plugin"


  provided "javax.xml.bind:jaxb-api:2.3.0"
  provided "org.grails:grails-gorm-testing-support"
  provided "org.grails:grails-web-testing-support"


  provided "org.postgresql:postgresql:42.2.14"
  
  compile "org.hibernate:hibernate-core:5.4.19.Final"
  compile "org.grails.plugins:views-json"
  compile 'com.ibm.icu:icu4j:62.1'

  provided 'org.grails.plugins:spring-security-core:4.0.2'
  
  compile 'io.github.http-builder-ng:http-builder-ng-core:1.0.4'

  provided "org.grails.plugins:geb"
  provided "org.seleniumhq.selenium:selenium-api:3.14.0"
  compile 'com.github.zafarkhaja:java-semver:0.9.0'
  
  compile ('org.grails.plugins:database-migration:3.1.0') {
    exclude group: 'org.liquibase', module: 'liquibase-core'
  }
  compile 'org.liquibase:liquibase-core:3.9.0'
  
  provided 'com.k_int.grails:web-toolkit-ce:6.0.0-SNAPSHOT'
}

bootRun {
  ignoreExitValue true
  jvmArgs(
    '-Dspring.output.ansi.enabled=always',
    '-noverify',
    '-XX:TieredStopAtLevel=1',
    '-Xmx1024m')
  sourceResources sourceSets.main
  String springProfilesActive = 'spring.profiles.active'
  systemProperty springProfilesActive, System.getProperty(springProfilesActive)
}

tasks.withType(GroovyCompile) {
  configure(groovyOptions) {
    forkOptions.jvmArgs = ['-Xmx1024m']
  }
}

// Writes plugin data
tasks.register("extendedConfigScript") { Task me ->
  
  // Find the correct file.
  Task configTask = project.tasks.getByName('configScript')
  File configFile = configTask.outputs.files.files.find { it.name == 'config.groovy' }
  if(!configFile) {
    return  // No need to continue if the file is not there
  }
  
  me.inputs.property('version', project.version)
  me.outputs.file(configFile)
  project.tasks.getByName('compileGroovy').dependsOn( me )
  me.dependsOn( configTask )
  
  doLast {
    configFile.append("""

/***** Extention *****/
withConfig(configuration) {
  inline(phase: 'CONVERSION') { source, context, classNode ->
    classNode.putNodeMetaData('projectVersion', '$project.version')
  }
}""")
  }
}


 
